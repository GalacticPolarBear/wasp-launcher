{$DEFINE WS_FORM_INCLUDED}
{$IFNDEF WS_LAUNCHER}
  {$I core.simba}
{$ENDIF}

(*
# WaspForm
Frontend form for the {ref}`WaspClient`.
*)
type
  TWaspForm = record
    Form: TForm;
    Size: TPoint;
    TokenTimer: TTimer;

    ReadClipboard: Boolean;

    Updating: record
      SRL: Boolean;
      WaspLib: Boolean;
      Script: Boolean;
    end;
  end;

procedure TWaspForm.HandleLogin(loginPanel: TPanel);
var
  msPanel, dPanel: TPanel;
  id, user: TLabel;
  scripts: TScriptDataArray;
  listBox: TListBox;
  i: Int32;
begin
  Self.TokenTimer.setEnabled(False);
  msPanel := Self.Form.GetChild('main_panel');
  dPanel := Self.Form.GetChild('discord_panel');

  user := msPanel.GetChild('user_label');
  id := msPanel.GetChild('id_label');

  user.setCaption('Logged as: ' + WaspClient.Cache.User.Username);
  id.setCaption('ID: ' + WaspClient.Cache.User.Id);

  scripts := WaspClient.GetAllUserScripts();

  listBox := msPanel.GetChild('scripts_listbox');

  listBox.Clear();
  for i := 0 to High(scripts) do
    listBox.getItems().Add(scripts[i].Name);

  listbox.setItemIndex(0);

  msPanel.Show();
  loginPanel.Hide();
  dPanel.Hide();
  Self._OnScriptChange(listBox);
end;

procedure TWaspForm.HandleLogout(msPanel: TPanel);
var
  loginPanel: TPanel;
begin
  loginPanel := Self.Form.GetChild('login_panel');

  if WaspConfig.GetBoolean('use_discord') then
    Self._DiscordButton(loginPanel.GetChild('discord_button'));
  msPanel.Hide();
end;


procedure TWaspForm._OpenScript(sender: TObject);
var
  panel: TPanel;
  list: TListBox;
  script: TScriptData;
  scriptFile: TScriptFile;
begin
  panel := TButton(sender).getOwner();
  list := panel.GetChild('scripts_listbox');
  script := WaspClient.GetAllUserScripts()[list.getItemIndex()];
  scriptFile := WaspUpdater.GetScriptFile(script.ID);
  SimbaOpenInTab(scriptFile.Path);
  Self.Form.Close();
end;

procedure TWaspForm._RunScript(sender: TObject);
var
  panel: TPanel;
  list: TListBox;
  script: TScriptData;
  scriptFile: TScriptFile;
  proc: TSysProc;
begin
  panel := TButton(sender).getOwner();
  list := panel.GetChild('scripts_listbox');
  script := WaspClient.GetAllUserScripts()[list.getItemIndex()];
  scriptFile := WaspUpdater.GetScriptFile(script.ID);
  SimbaRunInTab(scriptFile.Path);
  Self.Form.Close();

  for proc in {$H-}GetProcesses(){$H+} do
    if proc.PID = GetSimbaPID() then
    begin
      ShowWindow(proc.Handle, 6); //6 = SW_MINIMIZE https://learn.microsoft.com/en-us/windows/win32/api/winuser/nf-winuser-showwindow
      Break;
    end;
end;



procedure TWaspForm._MailField(sender: TObject; var key: char);
var
  panel: TPanel;
  email, password: TEdit;
  button: TButton;
  strMail: String;
begin
  email := sender;
  panel := email.getOwner().getOwner();
  button := panel.GetChild('login_button');
  password := panel.GetChild('password_edit');
  strMail := email.getText();
  button.setEnabled(strMail.IsEmail() and (password.GetTextLen() > 4));

  case key of
    ' ': key := #0;
    #13: TEdit(sender).PerformTab(True);
  end;
end;

procedure TWaspForm._PasswordField(sender: TObject; var key: char); overload;
var
  panel: TPanel;
  email: TEdit;
  button: TButton;
  strMail: String;
begin
  panel := TEdit(sender).getOwner().getOwner();
  button := panel.GetChild('login_button');
  email := panel.GetChild('email_edit');
  strMail := email.getText();
  button.setEnabled(strMail.IsEmail() and (TEdit(sender).GetTextLen() > 4));

  case key of
    ' ': key := #0;
    #13: TEdit(sender).PerformTab(True);
  end;
end;


procedure TWaspForm._UpdateSRL();
var
  panel: TPanel;
  caption, button: TComponent;
begin
  panel := Self.Form.GetChild('current_version_panel');
  caption := panel.GetChild('current_srl_label');
  panel := panel.GetOwner();
  button := panel.GetChild('update_srl_button');
  WaspUpdater.Update('srlt', caption, button);
end;

procedure TWaspForm._UpdateWL();
var
  panel: TPanel;
  caption, button: TComponent;
begin
  panel := Self.Form.GetChild('current_version_panel');
  caption := panel.GetChild('current_wl_label');

  panel := panel.GetOwner();
  button := panel.GetChild('update_wl_button');

  WaspUpdater.Update('wasplib', caption, button);
end;

procedure TWaspForm._UpdateScript();
var
  list: TListBox;
  script: TScriptData;
begin
  try
    Self.Updating.Script := True;
    list := Self.Form.GetChild('scripts_listbox');
    script := WaspClient.GetAllUserScripts()[list.getItemIndex()];
    WaspUpdater.UpdateScript(script);
    Self._OnScriptChange(list);
  finally
    Self.Updating.Script := False;
  end;
end;


procedure TWaspForm._OnTogglePass(sender: TObject);
var
  panel: TPanel;
  edit: TEdit;
  caption: TLabel;
  checkbox: TCheckBox;
begin
  checkbox := sender;
  panel   := checkbox.getParent();
  caption := panel.GetComponent(0);
  panel := panel.getParent();
  edit    := panel.GetChild('password_edit');

  if checkbox.IsChecked() then
  begin
    caption.setCaption('Hide password');
    edit.setPasswordChar(#0);
    Exit;
  end;

  caption.setCaption('Show password');
  edit.setPasswordChar('*');
end;


procedure TWaspForm._OnScriptChange(sender: TObject);
var
  list: TListBox;
  main, panel: TPanel;
  text: TLabel;
  memo: TMemo;
  update, start, open: TButton;
  checkbox: TCheckBox;
  scriptData: TScriptData;
  scriptFile: TScriptFile;
begin
  list := sender;
  scriptData := WaspClient.GetAllUserScripts()[list.getItemIndex()];
  scriptFile := WaspUpdater.GetScriptFile(scriptData.ID);

  main := list.getOwner().getOwner().getOwner().getOwner();

  panel := main.GetChild('version_panel');

  text := panel.GetChild('current_script_label');
  if scriptFile.Revision = 0 then
    text.setCaption('')
  else
    text.setCaption('rev.' + ToStr(scriptFile.Revision));

  if scriptFile.Revision <> scriptData.Revision then
    text.SetFontColor($1800E9)
  else
    text.SetFontColor($18E982);

  text := panel.GetChild('latest_script_label');
  text.setCaption('rev.' + ToStr(scriptData.Revision));

  if scriptFile.Revision <> scriptData.Revision then
    text.SetFontColor($1800E9)
  else
    text.SetFontColor($18E982);

  update := panel.GetChild('update_script_button');

  if scriptFile.Revision = 0 then
    update.setCaption('Install')
  else if scriptFile.Revision <> scriptData.Revision then
    update.setCaption('Update')
  else
    update.setCaption('Re-install');

  start  := main.GetChild('script_start_button');
  open  := main.GetChild('script_open_button');
  start.setEnabled(scriptFile.Revision <> 0);
  open.setEnabled(scriptFile.Revision <> 0);

  checkbox := panel.GetChild('update_script_checkbox');

  checkbox.SetChecked(WaspConfig.GetBoolean('update_' + scriptData.ID));
  if checkbox.IsChecked() and (scriptFile.Revision <> scriptData.Revision) then
  begin
    WaspUpdater.UpdateScript(scriptData);
    Self._OnScriptChange(sender);
    Exit;
  end;

  panel := main.GetChild('script_panel');
  text := panel.GetChild('script_name_label');
  text.setCaption(scriptData.Name);

  text := panel.GetChild('script_category_label');
  if scriptData.Categories.Contains('Premium') then
  begin
    text.setCaption('Premium');
    text.SetFontColor($4DB5FF);
  end
  else
  begin
    text.setCaption('Free');
    text.SetFontColor($F0F0F0);
  end;
  text.setLeft(panel.getWidth() - text.GetTrueWidth() - TControl.AdjustToDPI(5));

  memo := panel.GetChild('script_content_memo');
  memo.setText(scriptData.Description + LineEnding + LineEnding + scriptData.Content);

  if not FileExists(WaspClient.AssetsPath + scriptData.ID + DirectorySeparator + 'banner.jpg') then
    WaspClient.DownloadImage(scriptData.ID, 'banner');

  panel.SwapImage(WaspClient.AssetsPath + scriptData.ID + DirectorySeparator + 'banner.jpg');
end;


procedure TWaspForm.SetDarkTheme(sender: TWinControl);
var
  name: String;
begin
  name := sender.getName();
  if (name = '') or name.Contains('label') then Exit;

  if name.Contains('edit') then
    SetWindowTheme(sender.getHandle(), 'DarkMode_CFD')
  else
    SetWindowTheme(sender.getHandle(), 'DarkMode_Explorer');

  AllowDarkModeForWindow(sender.getHandle(), True);
end;

procedure TWaspForm.SetChildsDarkTheme(sender: TWinControl);
var
  i: Int32;
begin
  for i := 0 to sender.getComponentCount() - 1 do
    Self.SetChildsDarkTheme(sender.GetComponent(i));
  Self.SetDarkTheme(sender);
end;

procedure TWaspForm.SetAllChildsDarkTheme();
var
  i: Int32;
  child: TControl;
begin
  for i := 0 to Self.Form.getComponentCount() - 1 do
  begin
    child := Self.Form.GetComponent(i);
    Self.SetChildsDarkTheme(child);
  end;

  SetWindowTheme(Self.Form.getHandle(), 'DarkMode_Explorer');
  AllowDarkModeForWindow(Self.Form.getHandle(), True);
  RefreshImmersiveColorPolicyState();
  FlushMenuThemes();
end;


procedure TWaspForm.SetupDiscordPanel(form: TForm);
  type TWaspForm = TWaspForm;
  procedure TWaspForm._CancelButton(sender: TObject);
  var
    lPanel, dPanel: TPanel;
  begin
    WaspConfig.Put('use_discord', False);
    dPanel := TButton(sender).getOwner();
    lPanel := Self.Form.GetChild('login_panel');
    lPanel.Show();
    dPanel.Hide();
    Self.TokenTimer.setEnabled(False);
  end;

  procedure TWaspForm._ClipboardToggle(sender: TObject);
  begin
    Self.ReadClipboard := TCheckBox(sender).IsChecked();
  end;

var
  panel: TPanel;
  tokenEdit: TLabeledEdit;
  cancel: TButton;
  caption: TLabel;
  check: TLabeledCheckBox;
begin
  panel.Create(form);
  panel.setName('discord_panel');
  panel.setCaption('');
  panel.setWidth(TControl.AdjustToDPI(400));
  panel.setHeight(TControl.AdjustToDPI(150));
  panel.SetLeft(Self.Size.X div 2 - panel.getWidth() div 2);
  panel.SetTop(Self.Size.Y div 2 - (panel.getHeight() - (panel.getHeight() div 10)));
  panel.setBevelWidth(0);
  panel.Hide();

  tokenEdit.Create(panel);
  tokenEdit.SetName('discord_token');
  tokenEdit.SetCaption('Token:');
  tokenEdit.SetWidth(panel.getWidth());
  tokenEdit.SetControlColor($303030);
  tokenEdit.setControlFontColor($F0F0F0);

  check.Create(panel);
  check.SetName('clipboard_');
  check.SetCaption('Read clipboard');
  check.SetTop(tokenEdit.GetTop());
  if WaspConfig.JSON.has('clipboard_checkbox') then
  begin
    if WaspConfig.GetBoolean('clipboard_checkbox') then
      check.SetChecked(True);
  end
  else
    check.SetChecked(True);
  check.SetLeft(panel.getWidth() - check.GetWidth());

  check.CheckBox.setOnChange(@Self._ClipboardToggle);
  Self._ClipboardToggle(check.CheckBox);

  caption.Create(panel);
  caption.SetName('discord_label');
  caption.SetTop(tokenEdit.GetBottom() + TControl.AdjustToDPI(20));
  caption.SetCaption('Waiting for token');
  caption.SetLeft(panel.getWidth() div 2 - caption.GetTrueWidth() div 2);

  cancel.Create(panel);
  cancel.SetName('cancel_button');
  cancel.SetCaption('Cancel');
  cancel.setWidth(panel.getWidth() div 3);
  cancel.SetLeft(panel.getWidth() div 2 - cancel.getWidth() div 2);
  cancel.SetTop(panel.getHeight() - TControl.AdjustToDPI(40));
  cancel.setOnClick(@Self._CancelButton);
end;

procedure TWaspForm._OnTimer({$H-}sender: TObject);{$H+}
var
  panel: TPanel;
  caption: TLabel;
  edit: TEdit;
  token: String;
  i: Int32;
  {$IFDEF DEVELOPER_MODE}t: Double;{$ENDIF}
begin
  {$IFDEF DEVELOPER_MODE}t := PerformanceTimer();{$ENDIF}
  if Self.ReadClipboard then
  begin
    token := GetClipBoard();

    if token.IsRefreshToken() and WaspClient.Login(token) then
    begin
      Self.HandleLogin(Self.Form.GetChild('login_panel'));
      {$IFDEF DEVELOPER_MODE}
      WriteLn('Full login took: ', Round(PerformanceTimer()-t, 4), 'ms.');
      {$ENDIF}
      Exit;
    end;
  end;

  panel := Self.Form.GetChild('discord_panel');

  if not Self.ReadClipboard then
  begin
    edit := Self.Form.GetChild('discord_token_edit');
    token := edit.GetText();
    if token.IsRefreshToken() and WaspClient.Login(token) then
    begin
      Self.HandleLogin(Self.Form.GetChild('login_panel'));
      {$IFDEF DEVELOPER_MODE}
      WriteLn('Full login took: ', Round(PerformanceTimer()-t, 4), 'ms.');
      {$ENDIF}
      Exit;
    end;
  end;

  caption := panel.GetChild('discord_label');

  i := caption.GetTextLen() - 17 + 1;
  if i > 3 then i := 0;

  caption.SetCaption('Waiting for login' + '.' * i);
  caption.SetLeft(panel.getWidth() div 2 - caption.GetTrueWidth() div 2);
end;

procedure TWaspForm.RunTimer();
begin
  Self.TokenTimer.setOnTimer(@Self._OnTimer);
  Self.TokenTimer.setEnabled(True);
end;

procedure TWaspForm.SetupLoginPanel(form: TForm);
  type TWaspForm = TWaspForm;

  procedure TWaspForm._LoginButton(sender: TObject);
  var
    loginPanel, msPanel: TPanel;
    email, password: TEdit;
    {$IFDEF DEVELOPER_MODE}
    t: Double;
    {$ENDIF}
  begin
    {$IFDEF DEVELOPER_MODE}
    t := PerformanceTimer();
    {$ENDIF}

    loginPanel := TComponent(sender).getOwner();
    msPanel := Self.Form.GetChild('main_panel');

    email := loginPanel.GetChild('email_edit');
    password := loginPanel.GetChild('password_edit');

    if WaspClient.Login(email.getText(), password.getText()) then
    begin
      Self.HandleLogin(loginPanel);
      {$IFDEF DEVELOPER_MODE}
      WriteLn('Full login took: ', Round(PerformanceTimer()-t, 4), 'ms.');
      {$ENDIF}
      Exit;
    end;

    with email.getFont() do
    begin
      setColor($0000FF);
      setStyle([]);
    end;

    with password.getFont() do
    begin
      setColor($0000FF);
      setStyle([]);
    end;
  end;

  procedure TWaspForm._LogoutButton(sender: TObject);
  begin
    if WaspClient.Logout() then
      Self.HandleLogout(TButton(sender).getOwner());
  end;

  procedure TWaspForm._Validate(edit: TEdit);
  var
    panel: TPanel;
    email, password: TEdit;
    strMail: String;
    button: TButton;
  begin
    panel := edit.getOwner().getOwner();

    case edit.getName() of
      'email_edit':
      begin
        email := edit;
        password := panel.GetChild('password_edit');
      end;
      'password_edit':
      begin
        email := panel.GetChild('email_edit');
        password := edit;
      end;
      else
        TerminateScript('TWaspForm._Validate(), unknown control.');
    end;

    with email.getFont() do
    begin
      setStyle([fsBold]);
      setColor($F0F0F0);
    end;

    with password.getFont() do
    begin
      setStyle([fsBold]);
      setColor($F0F0F0);
    end;

    strMail := email.getText();
    button := TButton(panel.GetChild('login_button'));
    button.setEnabled(strMail.IsEmail() and (password.GetTextLen() > 4));

    if button.IsEnabled() then
      Self._LoginButton(button);
  end;

  procedure TWaspForm._OnEmailDone(sender: TObject);
  var
    email: TEdit;
    str: String;
  begin
    Self._Validate(sender);
    email := sender;
    str := LowerCase(Trim(email.getText()));
    if str.IsEmail() then
      WaspConfig.Put('email', LowerCase(Trim(email.getText())));
  end;

  procedure TWaspForm._OnEmailChange(sender: TObject);
  begin
    Self._OnEmailDone(sender);
  end;

  procedure TWaspForm._OnPassDone(sender: TObject);
  begin
    Self._Validate(sender);
  end;

  procedure TWaspForm._OnPassChange(sender: TObject);
  begin
    Self._OnPassDone(sender);
  end;

  procedure TWaspForm._DiscordButton(sender: TObject);
  var
    lPanel, dPanel: TPanel;
  begin
    WaspConfig.Put('use_discord', True);
    lPanel := TButton(sender).getOwner();

    dPanel := Self.Form.GetChild('discord_panel');

    dPanel.Show();
    lPanel.Hide();

    OpenWebPage('https://waspscripts.com/refresh_token/');
    Sync(@Self.RunTimer);
  end;
var
  panel: TPanel;
  mailEdit, passEdit: TLabeledEdit;
  login, discord: TButton;
  email: String;
  viewPass: TLabeledCheckBox;
begin
  panel.Create(form);
  panel.setName('login_panel');
  panel.setCaption('');
  panel.setWidth(TControl.AdjustToDPI(300));
  panel.setHeight(TControl.AdjustToDPI(180));
  panel.SetLeft(Self.Size.X div 2 - panel.getWidth() div 2);
  panel.SetTop(Self.Size.Y div 2 - (panel.getHeight() - (panel.getHeight() div 10)));
  panel.setBevelWidth(0);

  email := WaspConfig.GetString('email');
  if email = '' then
    email := 'email@mail.com';

  mailEdit.Create(panel);
  mailEdit.SetName('email');
  mailEdit.SetCaption('Email');
  mailEdit.SetWidth(panel.getWidth());
  mailEdit.SetText(email);
  mailEdit.SetControlColor($303030);
  mailEdit.setControlFontColor($F0F0F0);
  mailEdit.Edit.setOnKeyPress(@Self._MailField);
  mailEdit.Edit.setOnEditingDone(@Self._OnEmailDone);

  passEdit.Create(panel);
  passEdit.SetName('password');
  passEdit.SetCaption('Password');
  passEdit.SetTop(TControl.AdjustToDPI(50));
  passEdit.SetWidth(panel.getWidth());
  passEdit.SetControlColor($303030);
  passEdit.setControlFontColor($F0F0F0);
  passEdit.setPasswordChar('*');
  passEdit.Edit.setOnKeyPress(@Self._PasswordField);
  passEdit.Edit.setOnEditingDone(@Self._OnPassDone);
  passEdit.Edit.setOnChange(@Self._OnPassChange);

  viewPass.Create(panel);
  viewPass.SetName('viewpass');
  viewPass.SetCaption('Show password');
  viewPass.SetLeft(TControl.AdjustToDPI(10));
  viewPass.SetTop(TControl.AdjustToDPI(100));
  viewPass.CheckBox.setOnChange(@Self._OnTogglePass);

  Self._OnTogglePass(viewPass.CheckBox);

  login.Create(panel);
  login.SetName('login_button');
  login.SetCaption('Login');
  login.setWidth(panel.getWidth() div 3);
  login.SetTop(panel.getHeight() - (panel.getHeight() div 4));
  login.setEnabled(False);
  login.setOnClick(@Self._LoginButton);

  discord.Create(panel);
  discord.SetName('discord_button');
  discord.SetCaption('Discord login');
  discord.setWidth(login.getWidth());
  discord.SetLeft(panel.getWidth() - discord.getWidth());
  discord.SetTop(login.GetTop());
  discord.setOnClick(@Self._DiscordButton);
end;


procedure TWaspForm.SetupVersionsPanel(parent: TPanel; top: Int32);
  type TWaspForm = TWaspForm;

  function TWaspForm._VersionsTitleLabel(parent: TPanel; name: String; left: Int32): TPanel;
  var
    textLabel: TLabel;
  begin
    Result.Create(parent);
    Result.setName(LowerCase(name) + '_version_title_panel');
    Result.setCaption('');
    Result.setWidth(TControl.AdjustToDPI(110));
    Result.setHeight(parent.GetHeight());
    Result.SetLeft(left);
    Result.setBevelWidth(0);

    textLabel.Create(Result);
    textLabel.setName(LowerCase(name) + '_srl_title_label');
    textLabel.setCaption(name + ' SRL-T:');
    textLabel.setLeft(TControl.AdjustToDPI(3));
    textLabel.SetFontSize(10);
    textLabel.SetFontQuality(TFontQuality.fqCleartype);

    textLabel.Create(Result);
    textLabel.setName(LowerCase(name) + '_wl_title_label');
    textLabel.setCaption(name + ' WaspLib:');
    textLabel.setLeft(TControl.AdjustToDPI(3));
    textLabel.SetTop(TControl.AdjustToDPI(23));
    textLabel.SetFontSize(10);
    textLabel.SetFontQuality(TFontQuality.fqCleartype);

    textLabel.Create(Result);
    textLabel.setName(LowerCase(name) + '_script_title_label');
    textLabel.setCaption(name + ' Script:');
    textLabel.setLeft(TControl.AdjustToDPI(3));
    textLabel.SetTop(TControl.AdjustToDPI(46));
    textLabel.SetFontSize(10);
    textLabel.SetFontQuality(TFontQuality.fqCleartype);
  end;

  function TWaspForm._VersionsLabel(parent: TPanel; name, srlt, wl: String; left: Int32): TPanel;
  var
    textLabel: TLabel;
  begin
    Result.Create(parent);
    Result.setName(name + '_version_panel');
    Result.setCaption('');
    if name = 'current' then
      Result.setWidth(TControl.AdjustToDPI(90))
    else
      Result.setWidth(TControl.AdjustToDPI(70));
    Result.setHeight(parent.GetHeight());
    Result.SetLeft(left);
    Result.setBevelWidth(0);

    textLabel.Create(Result);
    textLabel.setName(name + '_srl_label');
    if srlt = '' then
      textLabel.setCaption('Missing')
    else if srlt = 'failed' then
      textLabel.setCaption('Rate limited')
    else
      textLabel.setCaption('v.' + srlt);
    textLabel.setLeft(TControl.AdjustToDPI(3));
    textLabel.SetFontSize(10);
    if srlt <> GitHubClient.Packages.Latest.SRLT then
      textLabel.SetFontColor($1800E9)
    else
      textLabel.SetFontColor($18E982);

    textLabel.Create(Result);
    textLabel.setName(name + '_wl_label');

    if wl = '' then
      textLabel.setCaption('Missing')
    else if wl = 'failed' then
      textLabel.setCaption('Rate limited')
    else
      textLabel.setCaption('v.' + wl);

    textLabel.setLeft(TControl.AdjustToDPI(3));
    textLabel.SetTop(TControl.AdjustToDPI(23));
    textLabel.SetFontSize(10);
    if wl <> GitHubClient.Packages.Latest.WaspLib then
      textLabel.SetFontColor($1800E9)
    else
      textLabel.SetFontColor($18E982);

    textLabel.Create(Result);
    textLabel.setName(name + '_script_label');
    textLabel.setLeft(TControl.AdjustToDPI(3));
    textLabel.SetTop(TControl.AdjustToDPI(46));
    textLabel.SetFontSize(10);
  end;

  function TWaspForm._VersionsControls(parent: TPanel; left: Int32): TPanel;
    type TWaspForm = TWaspForm;
    procedure TWaspForm._UpdateButton(sender: TObject);
    begin
      case TButton(sender).getName() of
        'update_srl_button':    {$IFNDEF DEVELOPER_MODE}Sync(@Self._UpdateSRL){$ENDIF};
        'update_wl_button':     {$IFNDEF DEVELOPER_MODE}Sync(@Self._UpdateWL){$ENDIF};
        'update_script_button': Self._UpdateScript;
      end;
    end;

    procedure TWaspForm._AutoUpdateToggle(sender: TObject);
    var
      panel: TPanel;
      button: TButton;
      list: TListBox;
      script: TScriptData;
    begin
      with TCheckBox(sender) do
        case GetName() of
          'update_script_checkbox':
          begin
            list := Self.Form.GetChild('scripts_listbox');
            script := WaspClient.GetAllUserScripts()[list.getItemIndex()];
            WaspConfig.Put('update_' + script.ID, IsChecked());
          end;

          else
          begin
            WaspConfig.Put(GetName(), IsChecked());
            panel := TCheckBox(sender).getParent().getParent();
            button := panel.GetChild(GetName().Replace('checkbox', 'button'));
            {$IFNDEF DEVELOPER_MODE}
            if IsChecked() and (button.getCaption() <> 'Re-install') then
              Self._UpdateButton(button);
            {$ENDIF}
          end;
        end;
    end;

  var
    button: TButton;
    check: TLabeledCheckBox;
  begin
    Result.Create(parent);
    Result.setName('version_buttons_panel');
    Result.setCaption('');
    Result.SetLeft(left);
    Result.setWidth(TControl.AdjustToDPI(180));
    Result.setHeight(parent.GetHeight());
    Result.setBevelWidth(0);

    button.Create(Result);
    button.setName('update_srl_button');
    button.setLeft(TControl.AdjustToDPI(3));
    button.setHeight(TControl.AdjustToDPI(20));
    button.setWidth(TControl.AdjustToDPI(70));
    if GitHubClient.Packages.Current.SRLT <> GitHubClient.Packages.Latest.SRLT then
      button.setCaption('Update')
    else
      button.setCaption('Re-install');
    button.setOnClick(@Self._UpdateButton);

    button.Create(Result);
    button.setName('update_wl_button');
    button.setLeft(TControl.AdjustToDPI(3));
    button.SetTop(TControl.AdjustToDPI(23));
    button.setHeight(TControl.AdjustToDPI(20));
    button.setWidth(TControl.AdjustToDPI(70));
    if GitHubClient.Packages.Current.WaspLib <> GitHubClient.Packages.Latest.WaspLib then
      button.setCaption('Update')
    else
      button.setCaption('Re-install');
    button.setOnClick(@Self._UpdateButton);

    button.Create(Result);
    button.setName('update_script_button');
    button.setLeft(TControl.AdjustToDPI(3));
    button.SetTop(TControl.AdjustToDPI(46));
    button.setHeight(TControl.AdjustToDPI(20));
    button.setWidth(TControl.AdjustToDPI(70));
    if GitHubClient.Packages.Current.WaspLib <> GitHubClient.Packages.Latest.WaspLib then
      button.setCaption('Update')
    else
      button.setCaption('Re-install');
    button.setOnClick(@Self._UpdateButton);

    check.Create(Result);
    check.SetName('update_srl');
    check.SetCaption('Auto-update');
    check.setLeft(TControl.AdjustToDPI(90));
    check.SetTop(TControl.AdjustToDPI(2));
    if WaspConfig.GetBoolean('update_srl_checkbox') then
      check.SetChecked(True);
    check.CheckBox.setOnChange(@Self._AutoUpdateToggle);
    Self._AutoUpdateToggle(check.CheckBox);

    check.Create(Result);
    check.SetName('update_wl');
    check.SetCaption('Auto-update');
    check.SetLeft(TControl.AdjustToDPI(90));
    check.SetTop(TControl.AdjustToDPI(25));
    if WaspConfig.GetBoolean('update_wl_checkbox') then
      check.SetChecked(True);
    check.CheckBox.setOnChange(@Self._AutoUpdateToggle);
    Self._AutoUpdateToggle(check.CheckBox);

    check.Create(Result);
    check.SetName('update_script');
    check.SetCaption('Auto-update');
    check.SetLeft(TControl.AdjustToDPI(90));
    check.SetTop(TControl.AdjustToDPI(48));
    check.CheckBox.setOnChange(@Self._AutoUpdateToggle);
  end;

var
  panel, tmp: TPanel;
  w, h: Int32;
begin
  panel.Create(parent);
  panel.setName('version_panel');
  panel.setCaption('');
  panel.setAlign(TAlign.alCustom);
  panel.setBorderStyle(TFormBorderStyle.bsNone);
  panel.setBevelWidth(0);
  panel.SetTop(top);
  panel.setHeight(TControl.AdjustToDPI(70));

  tmp := Self._VersionsTitleLabel(panel, 'Current', 0);
  w := tmp.getWidth();
  h := tmp.getHeight();
  tmp := Self._VersionsLabel(panel, 'current', GitHubClient.Packages.Current.SRLT, GitHubClient.Packages.Current.WaspLib, w);
  w += tmp.getWidth();
  h += tmp.getHeight();

  tmp := Self._VersionsTitleLabel(panel, 'Latest', w);
  w += tmp.getWidth();
  h += tmp.getHeight();
  tmp := Self._VersionsLabel(panel, 'latest', GitHubClient.Packages.Latest.SRLT, GitHubClient.Packages.Latest.WaspLib, w);
  w += tmp.getWidth();
  h += tmp.getHeight();

  tmp := Self._VersionsControls(panel, w);
  w += tmp.getWidth();
  h += tmp.getHeight();
  panel.setWidth(w + 20);
  panel.setLeft(Self.Size.X - TControl.AdjustToDPI(40) - w);
end;

procedure TWaspForm.SetupScriptPanel(parent: TPanel; topLeft: TPoint);
var
  panel, memoInnerPanel, memoOutterPanel: TPanel;
  text: TLabel;
  memo: TMemo;
  img: TImage;
begin
  panel.Create(parent);
  panel.setName('script_panel');
  panel.setCaption('');
  panel.SetLeft(topLeft.X);
  panel.SetTop(topLeft.Y);
  panel.setWidth(Self.Size.X - topLeft.X - TControl.AdjustToDPI(40));
  panel.setHeight(Self.Size.Y - topLeft.Y - TControl.AdjustToDPI(80));
  panel.setBevelWidth(0);

  img.Create(panel);
  img.SetBounds(0, 0, panel.getWidth(), TControl.AdjustToDPI(140));
  img.SetStretch(True);
  img.SetName('image_null');

  text.Create(panel);
  text.setName('script_name_label');
  text.SetFontSize(10);
  text.SetFontStyle([TFontStyle.fsBold]);
  text.setLeft(TControl.AdjustToDPI(5));
  text.SetTop(TControl.AdjustToDPI(115));

  text.Create(panel);
  text.setName('script_category_label');
  text.setLeft(-TControl.AdjustToDPI(5));
  text.SetTop(TControl.AdjustToDPI(115));

  memoOutterPanel.Create(panel);
  memoOutterPanel.setName('script_omemo_panel');
  memoOutterPanel.setCaption('');
  memoOutterPanel.SetTop(TControl.AdjustToDPI(145));
  memoOutterPanel.setWidth(panel.getWidth());
  memoOutterPanel.setHeight(panel.getHeight() - memoOutterPanel.GetTop());
  memoOutterPanel.setBevelWidth(0);
  memoOutterPanel.setColor($505050);

  memoInnerPanel.Create(memoOutterPanel);
  memoInnerPanel.setName('script_imemo_panel');
  memoInnerPanel.setCaption('');
  memoInnerPanel.SetTop(TControl.AdjustToDPI(2));
  memoInnerPanel.SetLeft(TControl.AdjustToDPI(2));
  memoInnerPanel.setWidth(memoOutterPanel.getWidth() - TControl.AdjustToDPI(4));
  memoInnerPanel.setHeight(memoOutterPanel.getHeight() - TControl.AdjustToDPI(4));
  memoInnerPanel.setBevelWidth(0);
  memoInnerPanel.setColor($303030);

  memo.Create(memoInnerPanel);
  memo.setName('script_content_memo');
  memo.setLeft(-TControl.AdjustToDPI(2));
  memo.SetTop(-TControl.AdjustToDPI(2));
  memo.setWidth(memoInnerPanel.getWidth() + TControl.AdjustToDPI(4));
  memo.setHeight(memoInnerPanel.getHeight() + TControl.AdjustToDPI(4));
  memo.setColor($303030);
  memo.SetFontColor($D5D5D5);
  memo.setScrollBars(TScrollStyle.ssAutoBoth);
  memo.setReadOnly(True);
  memo.setHideSelection(True);
  memo.setWordWrap(True);
end;


procedure TWaspForm.SetupMainPanel(form: TForm);
  type TWaspForm = TWaspForm;

  procedure TWaspForm._OnUserClick(sender: TObject);
  begin
    case TLabel(sender).getName() of
      'user_label': OpenWebPage('https://waspscripts.com/user/' + WaspClient.Cache.User.Id);
      'id_label': SetClipBoard(WaspClient.Cache.User.Id);
    end;
  end;

var
  panel: TPanel;
  user, id, launcherRev: TLabel;
  button: TButton;
  scriptsList: TLabeledDarkListBox;
begin
  panel.Create(form);
  panel.setName('main_panel');
  panel.setCaption('');
  panel.setWidth(Self.Size.X);
  panel.setHeight(Self.Size.Y);
  panel.setBevelWidth(0);

  user.Create(panel);
  user.setName('user_label');
  user.setCaption('');
  user.setLeft(TControl.AdjustToDPI(10));
  user.SetTop(TControl.AdjustToDPI(20));
  user.SetFontSize(12);
  user.setHint('Click to open your user profile.');
  user.setShowHint(True);
  user.setOnClick(@Self._OnUserClick);

  id.Create(panel);
  id.setName('id_label');
  id.setCaption('');
  id.setLeft(user.getLeft());
  id.SetTop(user.GetTop() + user.GetTrueHeight() + TControl.AdjustToDPI(20));
  id.SetFontSize(10);
  id.setHint('Click to copy the ID to the clipboard.');
  id.setShowHint(True);
  id.setOnClick(@Self._OnUserClick);

  scriptsList.Create(panel);
  scriptsList.SetName('scripts');
  scriptsList.SetCaption('Scripts:');
  scriptsList.SetLeft(id.getLeft());
  scriptsList.SetTop(id.GetTop() + id.getHeight() + TControl.AdjustToDPI(15));
  scriptsList.SetWidth(TControl.AdjustToDPI(260));
  scriptsList.setHeight(Self.Size.Y - scriptsList.GetTop() - TControl.AdjustToDPI(80));
  scriptsList.ListBox.ListBox.setOnClick(@Self._OnScriptChange);

  button.Create(panel);
  button.setName('logout_button');
  button.SetCaption('Logout');
  button.setOnClick(@Self._LogoutButton);
  button.setWidth(TControl.AdjustToDPI(150));
  button.SetLeft(TControl.AdjustToDPI(10));
  button.SetTop(panel.getHeight() - TControl.AdjustToDPI(51));

  button.Create(panel);
  button.setName('script_open_button');
  button.SetCaption('Open script');
  button.SetOnClick(@Self._OpenScript);
  button.setWidth(TControl.AdjustToDPI(150));
  button.SetLeft(panel.getWidth() - TControl.AdjustToDPI(10) - button.getWidth());
  button.SetTop(panel.getHeight() - TControl.AdjustToDPI(62));

  button.Create(panel);
  button.setName('script_start_button');
  button.SetCaption('Run script');
  button.setOnClick(@Self._RunScript);
  button.setWidth(TControl.AdjustToDPI(150));
  button.SetLeft(panel.getWidth() - TControl.AdjustToDPI(10) - button.getWidth());
  button.SetTop(panel.getHeight() - TControl.AdjustToDPI(40));

  launcherRev.Create(panel);
  launcherRev.setName('launcher_revision_label');
  launcherRev.setCaption('Launcher revision: ' + ToStr(WaspUpdater.Launcher.Revision));
  launcherRev.SetTop(panel.getHeight() - TControl.AdjustToDPI(30));
  launcherRev.setLeft(Self.Size.X div 2 - launcherRev.GetTrueWidth() div 2);

  Self.SetupVersionsPanel(panel, user.GetTop());
  Self.SetupScriptPanel(
    panel,
    [scriptsList.GetRight() + TControl.AdjustToDPI(30),
     scriptsList.GetTop()   + TControl.AdjustToDPI(30)]
  );
end;


procedure TWaspForm.OnShow(sender: TObject);
begin
  {$IFDEF WINDOWS}
  SetWindowDarkTitleBar(TForm(sender).getHandle());
  {$ENDIF}
  Self.SetAllChildsDarkTheme();

  case WaspClient.IsLoggedIn() of
    True:  Self.HandleLogin(Self.Form.GetChild('login_panel'));
    False: Self.HandleLogout(Self.Form.GetChild('main_panel'));
  end;
end;

procedure TWaspForm.Setup();
var
  launcherRev: TLabel;
begin
  Self.Size := [TControl.AdjustToDPI(900), TControl.AdjustToDPI(600)];

  Self.Form.Init(nil);
  Self.Form.SetName('wasp_form');
  Self.Form.setCaption('WaspScripts');
  Self.Form.setWidth(Self.Size.X);
  Self.Form.setHeight(Self.Size.Y);
  Self.Form.getConstraints().SetInterfaceConstraints(Self.Size.X, Self.Size.Y, Self.Size.X, Self.Size.Y);
  Self.Form.setPosition(TPosition.poScreenCenter);
  //Self.Form.setOnClose(@Self.OnClose);
  Self.Form.setColor($242322);
  Self.Form.setOnShow(@Self.OnShow);

  with Self.Form.getFont() do
  begin
    setQuality(TFontQuality.fqAntialiased);
    setPitch(TFontPitch.fpFixed);
    SetColor($F0F0F0);
  end;

  Self.TokenTimer.Init(nil);
  Self.TokenTimer.setName('token_timer');
  Self.TokenTimer.setInterval(1500);

  Self.SetupLoginPanel(Self.Form);
  Self.SetupDiscordPanel(Self.Form);
  Self.SetupMainPanel(Self.Form);

  launcherRev.Create(Self.Form);
  launcherRev.setName('launcher_revision_label');
  launcherRev.setCaption('Launcher revision: ' + ToStr(WaspUpdater.Launcher.Revision));
  launcherRev.SetTop(Self.Form.getHeight() - TControl.AdjustToDPI(30));
  launcherRev.setLeft(TControl.AdjustToDPI(Self.Size.X) div 2 - launcherRev.getWidth());
  launcherRev.setLeft(Self.Size.X div 2 - launcherRev.GetTrueWidth() div 2);
end;

procedure TWaspForm.Show();
begin
  try
    Self.Form.ShowModal();
  except
    Writeln(GetExceptionMessage());
  finally
    Self.Form.Free();
    Self.TokenTimer.setEnabled(False);
    Self.TokenTimer.Free();
  end;
end;

procedure TWaspForm.Run();
var
  uxtheme: TLibHandle;
begin
  uxtheme := LoadLibrary('uxtheme.dll');
  RefreshImmersiveColorPolicyState := GetProcAddr(uxtheme, PChar(104));
  ShouldAppUseDarkMode := GetProcAddr(uxtheme, PChar(132));
  AllowDarkModeForWindow := GetProcAddr(uxtheme, PChar(133));
  AllowDarkModeForApp := GetProcAddr(uxtheme, PChar(135));
  SetPreferredAppMode := GetProcAddr(uxtheme, PChar(135));
  FlushMenuThemes := GetProcAddr(uxtheme, PChar(136));

  AllowDarkModeForApp(True);
  SetPreferredAppMode(TPreferredAppMode.AllowDark);

  Self.Setup();
  Sync(@Self.Show);

  FreeLibrary(uxtheme);
  AllowDarkModeForWindow := nil;
  AllowDarkModeForApp := nil;
  SetPreferredAppMode := nil;
  FlushMenuThemes := nil;
  RefreshImmersiveColorPolicyState := nil;
end;

var
  WaspForm: TWaspForm;
